# Copyright (c) 2017, 2021 Eddie Antonio Santos <hello@eddieantonio.ca>
#
# Permission is hereby granted, free of charge, to any person obtaining a copy of
# this software and associated documentation files (the "Software"), to deal in
# the Software without restriction, including without limitation the rights to
# use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
# the Software, and to permit persons to whom the Software is furnished to do so,
# subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
# FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
# COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
# IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
# CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

# Determines if in a git repository, then places info in psvar.
#
# Derived from git-info by Sorin Ionescu in Prezto.
# https://github.com/sorin-ionescu/prezto/blob/d43bcb972091878efa3692100e8479fb887332f5/modules/git/functions/git-info
#
# Here's what's in psvar:
# %1v psvar[1]= the branch name, like "main" or "production"
# %2v psvar[2]= the ref, like "7c9168c853a9857a82c1913ca45694c3c723aa29"
# %3v psvar[3]= the number of unmerged files (UU)
# %4v psvar[4]= the number of untracked files (??)
# %5v psvar[5]= the number of files modified, but not added to the index
# %6v psvar[6]= the number of files added to the index
# %7v psvar[7]= the number of files deleted from the working tree
# %8v psvar[8]= the number of files that have been renamed
# %9v psvar[9]= the number of stash entries
function git-info-psvar {
  # Extended globbing is needed to parse repository status.
  setopt LOCAL_OPTIONS
  setopt EXTENDED_GLOB
  setopt WARN_CREATE_GLOBAL

  local branch_idx=1
  local ref_idx=2
  local unmerged_idx=3
  local untracked_idx=4
  local modified_idx=5
  local added_idx=6
  local deleted_idx=7
  local renamed_idx=8
  local stashed_idx=9

  local branch
  local -i stashed=0
  local -i untracked=0
  local -i unmerged=0
  local -i modified=0
  local -i added=0
  local -i deleted=0
  local -i renamed=0

  # Empty psvar. If psvar is empty, then we're not in a git repo.
  typeset -ga psvar
  psvar=()

  branch="$(git symbolic-ref --short HEAD 2> /dev/null)"

  # Quit early if not a git repository.
  if (( $#branch == 0 )); then
    return 0
  fi

  psvar[$branch_idx]="$branch"

  # TODO: support disabling on big repos.

  # Count added, modfied, renamed, deleted, untracked
  local line
  local status_cmd=""

  while IFS=$'\n' read line; do
    # Count added, deleted, modified, renamed, unmerged, untracked, dirty.
    # T (type change) is undocumented, see http://git.io/FnpMGw.
    # For an explanation of the two characters, see
    # see: https://git-scm.com/docs/git-status#_short_format
    # For a table of scenarii, see http://i.imgur.com/2YLu1.png.
    [[ "$line" == ([ACDMT][\ MT]|[ACMT]D)\ * ]] && (( added++ ))
    [[ "$line" == [\ ACMRT]D\ * ]] && (( deleted++ ))
    [[ "$line" == ?[MT]\ * ]] && (( modified++ ))
    [[ "$line" == R?\ * ]] && (( renamed++ ))
    [[ "$line" == (AA|DD|U?|?U)\ * ]] && (( unmerged++ ))
    [[ "$line" == \?\?\ * ]] && (( untracked++ ))
  done < <(git status --porcelain --ignore-submodules 2> /dev/null)

  # Insert value ONLY IF there are more than 0 instances.
  if (( added > 0 )); then
    psvar[$added_idx]="$added"
  fi

  if (( deleted > 0 )); then
    psvar[$deleted_idx]="$deleted"
  fi

  if (( modified > 0 )); then
    psvar[$modified_idx]="$modified"
  fi

  if (( renamed > 0 )); then
    psvar[$renamed_idx]="$renamed"
  fi

  if (( unmerged > 0 )); then
    psvar[$unmerged_idx]="$unmerged"
  fi

  if (( untracked > 0 )); then
    psvar[$untracked_idx]="$untracked"
  fi

  # Get the number of stashed changesets.
  stashed=$(( $(git stash list 2> /dev/null | wc -l) + 0))
  if (( stashed > 0 )); then
    psvar[$stashed_idx]="$stashed"
  fi
}

# vim: set ft=zsh:
